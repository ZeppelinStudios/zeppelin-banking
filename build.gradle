import java.util.regex.Pattern

plugins {
    id 'java'
    id 'xyz.jpenilla.run-paper' version '2.2.4'
    id 'com.github.johnrengelman.shadow' version '7.1.2'

}

group = 'xyz.zeppelin.bankaccounts'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    /* Jitpack for Simplix Storage */
    maven {
        url = "https://jitpack.io/"
    }

    maven { url = "https://repo.codemc.org/repository/maven-public/" }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"

    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'

    testCompileOnly 'org.projectlombok:lombok:1.18.32'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'

    compileOnly "de.tr7zw:item-nbt-api-plugin:2.12.3"
    implementation "dev.jorel:commandapi-bukkit-shade:9.4.0"

    compileOnly "com.github.MilkBowl:VaultAPI:1.7"

}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}


tasks {
    runServer {
        minecraftVersion '1.20.4'
    }
}

shadowJar {
    dependencies {
        include dependency("dev.jorel:commandapi-bukkit-shade:9.4.0")
    }

    relocate("com.zaxxer.hikaricp", "xyz.zeppelin.bankaccounts.shading.hikaricp")
    relocate("dev.jorel.commandapi", "xyz.zeppelin.bankaccounts.shading.cmd")
}

